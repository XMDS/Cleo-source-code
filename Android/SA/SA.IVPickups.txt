{$CLEO .csa}
0000:
//by: XMDS
//GTASA IV_Pickups
//2018.12.4
0DD6: 0@ = get_game_version
if
 0@ <> 13 // 13=GTASAv1.08
jf @DisableFunction
end_thread

:DisableFunction
//0DD9: write_mem_addr 0x0039339A value 0xBF00BF00 size 4 add_ib 1 protect 1 //CPickups::DoPickUpEffects(CEntity *)
0DD9: write_mem_addr 0x002D97D0 value 0xBF00BF00 size 4 add_ib 1 protect 1 //pos.z 
//0DD9: write_mem_addr 0x0039332C value 0xBF00BF00 size 4 add_ib 1 protect 1

0DD9: write_mem_addr 0x00392F3A value 0xBF00BF00 size 4 add_ib 1 protect 1 //CEntity::UpdateRwFrame(void)
0DD9: write_mem_addr 0x00392F34 value 0xBF00BF00 size 4 add_ib 1 protect 1 //CMatrix::UpdateRwMatrix(RwMatrixTag *)const

0DD9: write_mem_addr 0x002D9198 value 0xBF00BF00 size 4 add_ib 1 protect 1 //CCoronas::RegisterCorona

:Pickup
wait 0
10@ = 0x0070E264 //_ZN8CPickups8aPickUpsE
0085: 0@ = 10@
10@ += 0x4D60

:Main
0085: 5@ = 0@
5@ += 0x10
0DD8: 1@ = read_mem_addr 5@ size 2 add_ib 1 //X
1@ *= 0.125
0085: 5@ = 0@
5@ += 0x12
0DD8: 2@ = read_mem_addr 5@ size 2 add_ib 1 //Y
2@ *= 0.125
0085: 5@ = 0@
5@ += 0x14
0DD8: 3@ = read_mem_addr 5@ size 2 add_ib 1 //Z
3@ *= 0.125
0085: 5@ = 0@
5@ += 4
0DD8: 6@ = read_mem_addr 5@ size 4 add_ib 1 //[POINTER] - CObjectVC* Object
0085: 5@ = 0@
5@ += 0x18
0DD8: 7@ = read_mem_addr 5@ size 2 add_ib 1 //ModelId
if and
 1@ <> 0.0
 2@ <> 0.0
 3@ <> 0.0
 6@ <> 0
jf @while
0DD0: 8@ = get_label_addr @_ZN6CPools12GetObjectRefEP7CObject
0DD1: 8@ = get_func_addr_by_cstr_name 8@
0DD3: context_set_reg 0 value 6@
0DD2: context_call_func 8@
0DD4: 8@ = context_get_reg 0
if
03CA:   object 8@ exists
jf @while
01BB: store_object 8@ position_to 1@ 2@ 3@
if
00EC:   actor $PLAYER_ACTOR sphere 0 near_point 1@ 2@ radius 80.0 80.0 
jf @while
if or
00EC:   actor $PLAYER_ACTOR sphere 0 near_point 1@ 2@ radius 45.0 45.0 
02CC:   object 8@ bounding_sphere_visible 
jf @while
02CE: 4@ = ground_z_at 1@ 2@ 3@ 
4@ += 0.05
0061: 3@ -= 4@
if 
 3@ <= 5.0
jf @while
01BC: put_object 8@ at 1@ 2@ 4@ 
if
 7@ <> 1212 //money
jf @money
0453: set_object 8@ XYZ_rotation 90.0 0.0 134.0 
016F: create_particle 3 rotation_factor 0.0 size 0.5 intensity 100 flags 255 0 255 at 1@ 2@ 4@
jump @while

:money
0453: set_object 8@ XYZ_rotation 0.0 0.0 5.0
016F: create_particle 3 rotation_factor 0.0 size 0.5 intensity 255 flags 255 155 0 at 1@ 2@ 4@ 

:while
0@ += 0x20
if
001D:  0@ > 10@
jf @Main
jump @Pickup

:_ZN6CPools12GetObjectRefEP7CObject
hex
 "_ZN6CPools12GetObjectRefEP7CObject" 00
end